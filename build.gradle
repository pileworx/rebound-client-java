plugins {
    id "java"
    id "com.jfrog.artifactory" version "4.5.4"
    id "com.jfrog.bintray" version "1.8.4"
    id "maven-publish"
}

ext{
    bintrayUser = System.getProperty("bintray.user")
    bintrayKey = System.getProperty("bintray.key")
    buildNumber = System.getProperty("build.number")
}

group 'pileworx'
version '0.1.0'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile('com.mashape.unirest:unirest-java:1.4.9')
    compile('com.fasterxml.jackson.core:jackson-databind:2.9.8')
    compile('com.fasterxml.jackson.core:jackson-annotations:2.9.8')
    testCompile('junit:junit:4.12')
    testCompile('org.mockito:mockito-core:2.23.0')
    testCompile('org.hamcrest:hamcrest-core:1.3')
    testCompile('org.hamcrest:hamcrest-library:1.3')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    
    scm {
        url "https://github.com/pileworx/rebound-client-java"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId project.group
            artifactId rootProject.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Client library for rebound mock server')
                root.appendNode('name', 'Rebound Java Client')
                root.appendNode('url', 'https://github.com/pileworx/rebound-client-java')
                root.children().last() + pomConfig
            }
        }
    }
}

artifactory {
    contextUrl = 'http://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = bintrayUser
            password = bintrayKey
        }
        defaults {
            publications('mavenPublication')
            publishArtifacts = true
            publishPom = true
            properties = [
                    'build.number': buildNumber,
                    'build.name': rootProject.name
            ]
        }
    }
    resolve {
        repoKey = 'jcenter'
    }
    clientConfig.info.setBuildNumber(buildNumber)
    clientConfig.info.setBuildName(rootProject.name)
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['mavenPublication']

    pkg {
        repo = 'maven-release'
        name = rootProject.name
        userOrg = 'pileworx'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/pileworx/rebound-client-java'
        version {
            name = project.version
            desc = "build ${buildNumber}"
            released  = new Date()
            gpg {
                sign = true
            }
        }
    }

    publish = true
}

